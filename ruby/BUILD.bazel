# Protobuf Ruby runtime
#
# See also code generation logic under /src/google/protobuf/compiler/ruby.

load("@rules_pkg//:mappings.bzl", "pkg_files", "strip_prefix")
load("//build_defs:internal_shell.bzl", "inline_sh_test")
load("//conformance:defs.bzl", "conformance_test")
load("//:protobuf.bzl", "ruby_proto_library")

################################################################################
# Ruby Runtime
################################################################################

config_setting(
    name = "java_ruby",
    values = {"define": "ruby_platform=java"},
)

config_setting(
    name = "c_ruby",
    values = {"define": "ruby_platform=c"},
)

filegroup(
    name = "srcs",
    srcs = glob([
        "ext/google/protobuf_c/*",
        "lib/**/*.rb",
        "src/**/*.proto",
    ]) + [
        "Gemfile",
        "Rakefile",
        "google-protobuf.gemspec",
        "pom.xml",
    ],
)

genrule(
    name = "protobuf_c",
    srcs = [
        "Rakefile",
        ":srcs",
        ":test_ruby_protos",
        ":tests",
        "//third_party/utf8_range:all_files",
    ],
    tags = ["manual"],
    outs = ["lib/google/protobuf_c.so"],
    cmd = "pushd `dirname $(location Rakefile)`\n" +
          "BAZEL=true rake\n" +
          "popd\n" +
          "cp `dirname $(location Rakefile)`/lib/google/protobuf_c.so $(OUTS)\n",
)

genrule(
    name = "protobuf_java",
    srcs = glob(["src/**/*.java"]) + [
        "Rakefile",
        ":srcs",
        ":test_ruby_protos",
        ":tests",
        "//third_party/utf8_range:all_files",
    ],
    tags = ["manual"],
    outs = ["lib/google/protobuf_java.jar"],
    cmd = "pushd `dirname $(location Rakefile)`\n" +
          "BAZEL=true rake\n" +
          "popd\n" +
          "cp `dirname $(location Rakefile)`/lib/google/protobuf_java.jar $(OUTS)\n",
)

filegroup(
    name = "protobuf",
    srcs = [
        ":srcs",
        "//third_party/utf8_range:all_files",
    ] + select({
        ":java_ruby": [":protobuf_java"],
        "//conditions:default": [":protobuf_c"],
    }),
    visibility = [
        "//conformance:__subpackages__",
        "//ruby:__subpackages__",
    ],
)

################################################################################
# Tests
################################################################################

filegroup(
  name = "tests",
  srcs = glob(["tests/*.rb"]),
)

filegroup(
  name = "test_protos",
  srcs = glob(["tests/*.proto"]),
)

ruby_proto_library(
    name = "test_ruby_protos",
    srcs = [":test_protos"],
    proto_deps = ["//:well_known_protos"],
    includes = [".", "src", "ruby/tests"],
)

inline_sh_test(
    name = "test",
    srcs = [
        "Rakefile",
    ],
    deps = [
        ":protobuf",
        ":test_ruby_protos",
        ":tests",
        "//:well_known_ruby_protos",
    ],
    cmd = """
        pushd `dirname $(location Rakefile)`
        RUBYLIB=../src:tests:. BAZEL=true rake test
        popd
    """,
)

inline_sh_test(
    name = "gc_test",
    srcs = [
        "Rakefile",
    ],
    deps = [
        ":protobuf",
        ":test_ruby_protos",
        ":tests",
        "//:well_known_ruby_protos",
    ],
    cmd = """
        pushd `dirname $(location Rakefile)`
        RUBYLIB=../src:tests:. BAZEL=true rake gc_test
        popd
    """,
)

conformance_test(
    name = "conformance_test",
    failure_list = "//conformance:failure_list_ruby.txt",
    testee = "//conformance:conformance_ruby",
    text_format_failure_list = "//conformance:text_format_failure_list_ruby.txt",
)

conformance_test(
    name = "jruby_conformance_test",
    failure_list = "//conformance:failure_list_jruby.txt",
    testee = "//conformance:conformance_ruby",
    text_format_failure_list = "//conformance:text_format_failure_list_jruby.txt",
)

################################################################################
# Distribution files
################################################################################

pkg_files(
    name = "dist_files",
    srcs = glob([
        "compatibility_tests/v3.0.0/**/*",
        "ext/google/protobuf_c/*",
        "src/main/java/com/google/protobuf/jruby/*.java",
        "tests/*.proto",
        "tests/*.rb",
    ]) + [
        ".gitignore",
        "BUILD.bazel",
        "Gemfile",
        "README.md",
        "Rakefile",
        "google-protobuf.gemspec",
        "lib/google/protobuf.rb",
        "lib/google/protobuf/descriptor_dsl.rb",
        "lib/google/protobuf/message_exts.rb",
        "lib/google/protobuf/repeated_field.rb",
        "lib/google/protobuf/well_known_types.rb",
        "pom.xml",
        "src/main/java/google/ProtobufJavaService.java",
        "src/main/sentinel.proto",
        "travis-test.sh",
    ],
    strip_prefix = strip_prefix.from_root(""),
    visibility = ["//pkg:__pkg__"],
)
